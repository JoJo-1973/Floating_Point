#RetroDevStudio.MetaData.BASIC:2049,BASIC V2,uppercase,10,10
10 REM *** FLOATING POINT EXTRACTOR ***
20 REM *** (C) STEFANO PRIORE  2025 ***
30 :
40 DEF FNE(X)=X
50 POKE 53280,0:POKE53281,0:POKE646,8
60 PRINT"        FLOATING POINT CONVERTER"
70 PRINT:PRINT
80 PRINT"1  INPUT EXPRESSION":PRINT
90 PRINT"2  INPUT FORMULA":PRINT
100 PRINT"3  EXIT":PRINT
110 GET A$:IF A$<"1" OR A$>"3" THEN 110
120 ON VAL(A$) GOTO 140,2000,310
130 RUN
140 REM *** CONVERT EXPRESSION ***
150 PRINT"ENTER A FLOATING POINT EXPRESSION"
160 PRINT"RETURN TO FINISH":GOSUB 7000
170 EX$=""
180 INPUT"EXPRESSION ";EX$
190 IF EX$="" THEN RUN
200 PRINT"":PRINT:PRINT:POKE 646,0
210 PRINT"6010 V=";EX$;":EX$=";CHR$(34);EX$;CHR$(34)
220 PRINT"RUN 250"
230 PRINT"";
240 POKE 631,13:POKE632,13:POKE 633,13:POKE 198,3:END
250 GOSUB 6000
260 POKE 646,8
270 PRINT"EXPRESSION: ";EX$
280 PRINT"VALUE     :";V
290 PRINT:PRINT"FLOATING POINT FORMAT":PRINT
300 GOSUB 4000:GOSUB 5000:GOSUB 7000:GOTO 180
310 REM *** END OF PROGRAM ***
320 PRINT"";:END
330 :
4000 REM *** PRINT FLOATING POINT FORMAT ***
4010 FOR I=0 TO 4
4020 BY=PEEK(AD+I):GOSUB 8000
4030 PRINT X$;" ";
4040 NEXT I:PRINT
4050 RETURN
4060 :
5000 REM *** PRINT FAC FORMAT ***
5010 FOR I=0 TO 4
5020 IF I=1 THEN BY=PEEK(AD+I) OR 128:GOTO 5040
5030 BY=PEEK(AD+I)
5040 GOSUB 8000
5050 PRINT X$;" ";
5060 NEXT I:IF V<0 THEN PRINT"$FF":RETURN
5070 PRINT "$00":RETURN
5080 :
6000 REM *** EVALUATE EXPRESSION ***
6010 V=0:EX$=""
6020 AD=(PEEK(45)+PEEK(46)*256)+2:REM ADDRESS OF VARIABLE V'S PAYLOAD
6030 RETURN
6040 :
7000 REM *** WAIT FOR KEYPRESS ***
7010 POKE 781,23:POKE 782,0:SYS 65520
7020 PRINT"PRESS ANY KEY TO CONTINUE";
7030 POKE 198,0:WAIT 198,1:POKE 198,0
7040 RETURN
7050 :
8000 REM *** CONVERT BYTE TO HEX ***
8010 X$="$":HI=INT(BY/16):LO=BY-16*HI
8020 X$=X$+MID$("0123456789ABCDEF",HI+1,1)
8030 X$=X$+MID$("0123456789ABCDEF",LO+1,1)
8040 RETURN
