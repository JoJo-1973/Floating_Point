#RetroDevStudio.MetaData.BASIC:2049,BASIC V2,uppercase,10,10
10 REM *** FLOATING POINT EXTRACTOR ***
20 REM *** (C) STEFANO PRIORE  2025 ***
30 :
40 DEF FNE(X)=X
50 POKE 53280,0:POKE53281,0:POKE646,8
60 PRINT"        FLOATING POINT CONVERTER"
70 PRINT:PRINT
80 PRINT"1  INPUT EXPRESSION":PRINT
90 PRINT"2  INPUT FORMULA":PRINT
100 PRINT"3  EXIT":PRINT

110 GET A$:IF A$<"1" OR A$>"3" THEN 110
120 ON VAL(A$) GOTO 140,2000,310
130 RUN
140 REM *** CONVERT EXPRESSION ***
150 PRINT"ENTER A FLOATING POINT EXPRESSION"
160 PRINT"RETURN TO FINISH":GOSUB 420
170 EX$=""
180 INPUT"EXPRESSION ";EX$
190 IF EX$="" THEN RUN
200 PRINT"":PRINT:PRINT:POKE 646,0
210 PRINT"390 V=";EX$;":EX$=";CHR$(34);EX$;CHR$(34)
220 PRINT"RUN 250"
230 PRINT"";
240 POKE 631,13:POKE632,13:POKE 633,13:POKE 198,3:END
250 GOSUB 380
260 POKE 646,8
270 PRINT"EXPRESSION: ";EX$
280 PRINT"VALUE     :";V
290 PRINT:PRINT"FLOATING POINT FORMAT":PRINT
300 GOSUB 330:GOSUB 420:GOTO 180
310 REM *** END OF PROGRAM ***
320 PRINT"";:END
330 REM *** PRINT FLOATING POINT BYTES ***
340 FOR I=AD TO AD+4
350 GOSUB 460:PRINT X$;" ";
360 NEXT I:PRINT
370 RETURN
380 REM *** EVALUATE EXPRESSION ***
390 V=0:EX$=""
400 AD=(PEEK(45)+PEEK(46)*256)+2:REM ADDRESS OF VARIABLE V'S PAYLOAD
410 RETURN
420 POKE 781,23:POKE 782,0:SYS 65520
430 PRINT"PRESS ANY KEY TO CONTINUE";
440 POKE 198,0:WAIT 198,1:POKE 198,0
450 RETURN
460 REM *** CONVERT BYTE TO HEX ***
470 X$="$":H=INT(PEEK(I)/16):L=PEEK(I)-16*H
480 X$=X$+MID$("0123456789ABCDEF",H+1,1)
490 X$=X$+MID$("0123456789ABCDEF",L+1,1)
500 RETURN
